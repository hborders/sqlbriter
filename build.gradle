buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 29,
      'lint': '26.5.1'
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.2'
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  // Android dependencies.
  supportV4 = 'com.android.support:support-v4:28.0.0'
  supportAnnotations = 'com.android.support:support-annotations:28.0.0'
  supportTestRunner = 'com.android.support.test:runner:1.0.2'

  supportSqlite = 'android.arch.persistence:db:1.1.1'
  supportSqliteFramework = 'android.arch.persistence:db-framework:1.1.1'

  // Third-party dependencies.
  dagger = 'com.google.dagger:dagger:2.13'
  daggerCompiler = 'com.google.dagger:dagger-compiler:2.13'
  butterKnifeRuntime = 'com.jakewharton:butterknife:8.8.1'
  butterKnifeCompiler = 'com.jakewharton:butterknife-compiler:8.8.1'
  timber = 'com.jakewharton.timber:timber:4.6.0'
  autoValue = 'com.google.auto.value:auto-value:1.5'
  autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
  rxJava = 'io.reactivex.rxjava2:rxjava:2.1.3'
  rxAndroid = 'io.reactivex.rxjava2:rxandroid:2.0.1'
  rxBinding = 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:0.36'

  // Lint dependencies.
  lintApi = "com.android.tools.lint:lint-api:${versions.lint}"
  lint = "com.android.tools.lint:lint:${versions.lint}"
  lintTests = "com.android.tools.lint:lint-tests:${versions.lint}"
}

configurations {
  osstrich
}
dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.2.0'
}
task publishV1Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite'
  ]
}
task publishV2Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite2'
  ]
}
task publishV3Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite3'
  ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc, publishV3Javadoc])
