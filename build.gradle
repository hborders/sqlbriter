buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 29,
      'lint': '26.5.1'
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.2'
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  // Android dependencies.
  supportFragment = 'androidx.fragment:fragment:1.1.0'
  supportAnnotations = 'androidx.annotation:annotation:1.1.0'
  supportTestRunner = 'androidx.test.ext:junit:1.1.1'
  androidxTestRunner = 'androidx.test:runner:1.2.0' // for androidx.test.filters

  supportSqlite = 'androidx.sqlite:sqlite:2.0.1'
  supportSqliteFramework = 'androidx.sqlite:sqlite-framework:2.0.1'

  // Third-party dependencies.
  dagger = 'com.google.dagger:dagger:2.24'
  daggerCompiler = 'com.google.dagger:dagger-compiler:2.24'
  timber = 'com.jakewharton.timber:timber:4.7.1'
  autoValue = 'com.google.auto.value:auto-value:1.7'
  autoValueAnnotations = 'com.google.auto.value:auto-value-annotations:1.7'
  autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.8'
  rxJava = 'io.reactivex.rxjava2:rxjava:2.2.13'
  rxAndroid = 'io.reactivex.rxjava2:rxandroid:2.1.1'
  rxBinding = 'com.jakewharton.rxbinding2:rxbinding:2.2.0'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:1.0'

  // Lint dependencies.
  lintApi = "com.android.tools.lint:lint-api:${versions.lint}"
  lint = "com.android.tools.lint:lint:${versions.lint}"
  lintTests = "com.android.tools.lint:lint-tests:${versions.lint}"

  // Checker dependencies.
  // Can't upgrade to 3.0.0 because of
  // https://github.com/typetools/checker-framework/issues/2854
  checkerFrameworkVersion = "2.11.1"
  checkerQualAndroid = "org.checkerframework:checker-qual-android:${checkerFrameworkVersion}"
  checker = "org.checkerframework:checker:${checkerFrameworkVersion}"
  checkerJdk8 = "org.checkerframework:jdk8:${checkerFrameworkVersion}"
  errorproneJavac = "com.google.errorprone:javac:9+181-r4173-1"
}

configurations {
  osstrich
}
dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.4.0'
}
task publishV1Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite'
  ]
}
task publishV2Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite2'
  ]
}
task publishV3Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite3'
  ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc, publishV3Javadoc])
